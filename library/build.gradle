plugins {
    id 'org.jetbrains.kotlin.jvm' version '1.3.72'
    id 'maven'
    id 'maven-publish'
    id 'com.jfrog.bintray' version '1.8.5'
}

repositories {
    apply from: "${rootProject.projectDir}/dependencies.gradle"
    mavenCentral()
}


group 'wu.seal'
version "$versions.j2k_version"

task sourcesJar(type: Jar, dependsOn: classes) {
    archiveClassifier.set('sources')
    from sourceSets.main.allSource
}

javadoc.failOnError = false
task javadocJar(type: Jar, dependsOn: javadoc) {
    archiveClassifier.set('javadoc')
    from javadoc.destinationDir
}


artifacts {
    archives sourcesJar
    archives javadocJar
}


dependencies {
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk8"



    // Gson : Gson
    implementation 'com.google.code.gson:gson:2.8.6'

    // Test dependencies
    testImplementation "junit:junit:4.12"
    testImplementation('com.winterbe:expekt:0.5.0') {
        exclude group: "org.jetbrains.kotlin"
    }

    // Core library
    api files('libs/JsonToKotlinClass-3.6.1.jar')
}

compileKotlin {
    kotlinOptions.jvmTarget = "1.8"
}
compileTestKotlin {
    kotlinOptions.jvmTarget = "1.8"
}

//create a single Jar with all dependencies
task fatJar(type: Jar) {
    manifest {
        attributes 'Implementation-Title': 'JsonToKotlin Library',
                'Implementation-Version': version,
                'Main-Class': ''
    }
    baseName = project.name
    from { configurations.compile.collect { it.isDirectory() ? it : zipTree(it) } }
    with jar
}


def pomConfig = {
    licenses {
        license {
            name "GNU General Public License v3.0"
            url "https://www.gnu.org/licenses/gpl-3.0.en.html"
            distribution "repo"
        }
    }
    developers {
        developer {
            id "shifarshifz"
            name "shifarshifz"
            email "theapache64@gmail.com"
        }
    }

    scm {
        url "https://github.com/wuseal/JsonToKotlinClass"
    }
}

publishing {
    publications {
        mavenPublication(MavenPublication) {
            from components.java
            artifact sourcesJar {
                classifier "sources"
            }
            artifact javadocJar {
                classifier "javadoc"
            }
            groupId 'wu.seal'
            artifactId 'jsontokotlin'
            version "$versions.j2k_version"
            pom.withXml {
                def root = asNode()
                root.appendNode('description', 'To generate Kotlin data class code from JSON')
                root.appendNode('name', 'JsonToKotlinClass')
                root.appendNode('url', 'https://github.com/wuseal/JsonToKotlinClass')
                root.children().last() + pomConfig
            }
        }
    }
}

bintray {
    user = System.getenv('BINTRAY_USER')
    key = System.getenv('BINTRAY_API_KEY')
    publications = ['mavenPublication']

    pkg {
        repo = 'maven'
        name = 'JsonToKotlinClass'
        userOrg = 'shifarshifz'
        licenses = ['GPL-3.0']
        vcsUrl = 'https://github.com/wuseal/JsonToKotlinClass.git'
        version {
            name = versions.j2k_version
            desc = versions.j2k_version
            released = new Date()
        }
    }

}