plugins {
    id 'org.jetbrains.kotlin.jvm' version '1.3.72'
    id 'maven'
    id 'maven-publish'
    id 'signing'
}

repositories {
    apply from: "${rootProject.projectDir}/dependencies.gradle"
    mavenCentral()
}

group 'wu.seal'
version "$versions.j2k_version"


dependencies {
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk8"

    // Core library
    compile files('libs/JsonToKotlinClass-3.7.4.jar')

    // Gson : Gson
    implementation 'com.google.code.gson:gson:2.8.6'

    // Test dependencies
    testImplementation "junit:junit:4.12"

    // Expekt : An assertion library for Kotlin
    testImplementation('com.winterbe:expekt:0.5.0'){
        exclude group: "org.jetbrains.kotlin"
    }
}

jar {
    from { configurations.compile.collect { it.isDirectory() ? it : zipTree(it) } }
}

task sourcesJar(type: Jar, dependsOn: classes) {
    archiveClassifier.set('sources')
    from sourceSets.main.allSource
}

javadoc.failOnError = false
task javadocJar(type: Jar, dependsOn: javadoc) {
    archiveClassifier.set('javadoc')
    from javadoc.destinationDir
}

compileKotlin {
    kotlinOptions.jvmTarget = "1.8"
}
compileTestKotlin {
    kotlinOptions.jvmTarget = "1.8"
}



artifacts {
    archives sourcesJar
    archives javadocJar
}

def pomConfig = {
    licenses {
        license {
            name "GNU General Public License v3.0"
            url "https://www.gnu.org/licenses/gpl-3.0.en.html"
            distribution "repo"
        }
    }
    developers {
        developer {
            id "wuseal"
            name "wuseal"
            email "wusealking@gmail.com"
        }
    }

    scm {
        url "https://github.com/wuseal/JsonToKotlinClass"
    }
}

publishing {
    publications {
        mavenPublication(MavenPublication) {
            from components.java

            artifact sourcesJar {
                classifier "sources"
            }
            artifact javadocJar {
                classifier "javadoc"
            }
            groupId 'com.sealwu.jsontokotlin'
            artifactId 'library'
            version "$versions.j2k_version"
            pom.withXml {
                def root = asNode()
                root.appendNode('description', 'To generate Kotlin data class code from JSON')
                root.appendNode('name', 'JsonToKotlinClass')
                root.appendNode('url', 'https://github.com/wuseal/JsonToKotlinClass')
                root.children().last() + pomConfig
            }
        }
    }
    repositories {
        maven {
            name 'mavenCentral'
            // change URLs to point to your repos, e.g. http://my.org/repo
            def snapshotsRepoUrl = 'https://s01.oss.sonatype.org/content/repositories/snapshots/'
            def releasesRepoUrl = 'https://s01.oss.sonatype.org/service/local/staging/deploy/maven2/'
            url = version.endsWith('SNAPSHOT') ? snapshotsRepoUrl : releasesRepoUrl
            credentials(PasswordCredentials)
        }
    }
}
signing {
    sign publishing.publications.mavenPublication
}
